# Process this file with autoconf to produce a configure script.
AC_PREREQ(2.52)
AC_INIT(bison, 1.28c, bug-bison@gnu.org)
AC_CONFIG_AUX_DIR(config)
AM_INIT_AUTOMAKE(bison, 1.28c)
AM_CONFIG_HEADER(config.h:config.hin)

# Initialize the test suite.
AT_CONFIG(../src)

# Checks for programs.
AC_PROG_CC
AC_MINIX
AC_ISC_POSIX
AM_PROG_CC_STDC
AC_PROG_RANLIB
# GNU M4 is needed to build the testsuite.
BISON_AC_PROG_GNU_M4
if test "$ac_cv_prog_gnu_m4" != yes; then
  missing_dir=`cd $ac_aux_dir && pwd`
  M4="$missing_dir/missing m4"
fi

AC_ARG_ENABLE(gcc-warnings,
[  --enable-gcc-warnings   turn on lots of GCC warnings (not recommended)],
[case "${enableval}" in
   yes|no) ;;
   *)      AC_MSG_ERROR([bad value ${enableval} for gcc-warnings option]) ;;
 esac],
              [enableval=no])
if test "${enableval}" = yes; then
  BISON_WARNING(-Wall)
  BISON_WARNING(-W)
  BISON_WARNING(-Wbad-function-cast)
  BISON_WARNING(-Wcast-align)
  BISON_WARNING(-Wcast-qual)
  BISON_WARNING(-Wformat)
  BISON_WARNING(-Wmissing-declarations)
  BISON_WARNING(-Wmissing-prototypes)
  BISON_WARNING(-Wnested-externs)
  BISON_WARNING(-Wshadow)
  BISON_WARNING(-Wstrict-prototypes)
  BISON_WARNING(-Wwrite-strings)
  CFLAGS="$CFLAGS $WARNING_CFLAGS"
fi

# Checks for libraries.
AC_DEFINE([_GNU_SOURCE],1,[Define to 1 for GNU C library extensions.])

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(ctype.h locale.h memory.h stdlib.h string.h unistd.h)

# Checks for typedefs.

# Checks for structures.

# Checks for compiler characteristics.
AC_C_CONST
AC_C_INLINE
AM_C_PROTOTYPES

# Checks for library functions.
AC_FUNC_ALLOCA
AC_CHECK_FUNCS(mkstemp setlocale)
AC_CHECK_DECLS([stpcpy, strndup, strnlen, memchr])
AC_REPLACE_FUNCS(stpcpy strndup strnlen strspn memchr)
jm_FUNC_MALLOC
jm_FUNC_REALLOC
jm_PREREQ_QUOTEARG
jm_PREREQ_ERROR

ALL_LINGUAS="de es et fr ja nl ru"
AM_GNU_GETTEXT

# This is necessary so that .o files in LIBOBJS are also built via
# the ANSI2KNR-filtering rules.
LIBOBJS=`echo $LIBOBJS|sed 's/\.o /\$U.o /g;s/\.o$/\$U.o/'`
AC_SUBST(LIBOBJS)

AC_OUTPUT([Makefile
           config/Makefile
           intl/Makefile po/Makefile.in
           lib/Makefile src/Makefile doc/Makefile
           m4/Makefile
           tests/Makefile tests/atconfig])
