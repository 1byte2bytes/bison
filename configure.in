dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.13)
AC_INIT(src/reduce.c)
AM_INIT_AUTOMAKE(bison, 1.28a)
AM_CONFIG_HEADER(config.h:config.hin)
AC_DEFINE_UNQUOTED(VERSION_STRING, "GNU Bison version ${VERSION}",
                   [The version string.])

ALL_LINGUAS="de es et fr ja nl ru"

dnl Checks for programs.
AC_PROG_CC
AC_MINIX
AC_ISC_POSIX
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_PROG_RANLIB

AC_ARG_ENABLE(gcc-warnings,
[  --enable-gcc-warnings   turn on lots of GCC warnings (not recommended)],
[case "${enableval}" in
   yes|no) ;;
   *)      AC_MSG_ERROR([bad value ${enableval} for gcc-warnings option]) ;;
 esac
],
enableval=no
)
if test "${enableval}" = yes; then
  BISON_WARNING(-Wall)
  BISON_WARNING(-W)
  BISON_WARNING(-Wbad-function-cast)
  BISON_WARNING(-Wcast-align)
  BISON_WARNING(-Wcast-qual)
  BISON_WARNING(-Wmissing-declarations)
  BISON_WARNING(-Wmissing-prototypes)
  BISON_WARNING(-Wnested-externs)
  BISON_WARNING(-Wno-char-subscripts)
  BISON_WARNING(-Wstrict-prototypes)
  BISON_WARNING(-Wwrite-strings)
fi

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(ctype.h locale.h memory.h stdlib.h string.h unistd.h)

dnl Checks for typedefs.

dnl Checks for structures.

dnl Checks for compiler characteristics.
AC_C_CONST
AM_C_PROTOTYPES

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_CHECK_FUNCS(mkstemp setlocale)
BISON_NEED_DECLARATIONS(calloc realloc)

AM_GNU_GETTEXT
jm_PREREQ_ERROR

# This is necessary so that .o files in LIBOBJS are also built via
# the ANSI2KNR-filtering rules.
LIBOBJS=`echo $LIBOBJS|sed 's/\.o /\$U.o /g;s/\.o$/\$U.o/'`
AC_SUBST(LIBOBJS)

AC_OUTPUT([Makefile
           intl/Makefile po/Makefile.in
           lib/Makefile src/Makefile doc/Makefile m4/Makefile])
